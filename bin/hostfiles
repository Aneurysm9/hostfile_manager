#! /usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;
use Hostfile::Manager;

my @enabled = ();
my @disabled = ();
my $status = 0;
my $help = 0;
my $man = 0;

GetOptions(
	"enable=s" => \@enabled,
	"disable=s" => \@disabled,
	"status" => \$status,
	"help|?" => \$help,
	man => \$man,
) or pod2usage(2);

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

@enabled = split(/,/, join(',', @enabled));
@disabled = split(/,/, join(',', @disabled));

pod2usage(1) unless (@enabled || @disabled || $status);

my $manager = Hostfile::Manager->new;

map { print "Disabling $_\n"; $manager->disable_fragment($_) } @disabled;
map { print "Enabling $_\n"; $manager->enable_fragment($_) } @enabled;

$manager->write_hostfile if (@disabled || @enabled);

if ($status)
{
	foreach my $fragment ($manager->fragment_list)
	{
		my $fragment_contents = $manager->get_fragment($fragment);

		my $found = $manager->hostfile =~ /@{[$manager->block($fragment)]}/g;
		my $flag = $found ? ($1 eq $fragment_contents ? "+" : "*") : " ";
		print "$flag $fragment\n";
	}
}

__END__

=head1 NAME

hostfiles - A simple script to manage multiple sets of hostfiles on *NIX systems

=head1 SYNOPSIS

hostfiles [options]

  Options:
    --enable	Enable one or more hostfile fragments
    --disable	Disable one or more hostfile fragments
    --status	Display the status of various hostfile fragments

=head1 OPTIONS

=over 8

=item B<--enable>

Enable one or more hostfile fragments.  This option may be specified multiple times and multiple fragments may be specified in a comma-separated list.

=item B<--disable>

Disable one or more hostfile fragments.  This option may be specified multiple times and multiple fragments may be specified in a comma-separated list.

=item B<--status>

Display a list of fragments and their status.  A '+' indicates the fragment is enabled.  A '*' indicates an enabled fragment has been changed in /etc/hosts.

=back

=head1 DESCRIPTION

B<This program> will read the hostfile fragments specified to be enabled from /etc/hostfiles/<fragment_name> and add them to /etc/hosts.  It will remove any fragments from /etc/hosts that are specified to be disabled.

=cut
